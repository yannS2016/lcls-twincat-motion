<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="FB_ParamsSaveRestoreNC" Id="{7f8d36cc-18a1-4d6c-99dd-bd0795394dcd}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_ParamsSaveRestoreNC IMPLEMENTS I_ParamSaveRestore
VAR
    {attribute 'no_copy'}
    AxisRef : REFERENCE TO AXIS_REF;
    {attribute 'hide'}
    iPersistentDataStorage : I_PersistentDataStorage;
    // save and restore
    {attribute 'hide'}
    fbSetPos: MC_SetPosition;
    {attribute 'hide'}
    bClearPositionLag : BOOL := TRUE;
    {attribute 'hide'}
    nLatchErrId : BOOL;
    {attribute 'hide'}
    bRestoreDone 	: BOOL;
    {attribute 'hide'}
    bRestoreError 	: BOOL;
    {attribute 'hide'}
    nRestoreErrorID : UDINT;
    {attribute 'hide'}
    // save and Restore
    RestoreTimer: TON;
    {attribute 'hide'}
    bRestoreInit: BOOL;
    {attribute 'hide'}
    bRestoreLoad: BOOL;
    {attribute 'hide'}
    nLatchError: UDINT;
    {attribute 'hide'}
    bEncError: BOOL;
    {attribute 'hide'}
    tRetryDelay: TIME := T#1S;
    {attribute 'hide'}
    tRefreshDelay: TIME := T#1S;
    {attribute 'hide'}
    bUpdateEnable : BOOL;
    {attribute 'hide'}
    bExecute : BOOL;
    {attribute 'hide'}
    nMaxRetries: UINT := 10;
    {attribute 'hide'}
    nCurrTries: UINT := 0;
    {attribute 'hide'}
    bRestoreWaitRetry: BOOL;
    {attribute 'hide'}
    tonRestoreRetry: TON;
    {attribute 'hide'}
    tRefreshTime : TIME := T#1S;
    {attribute 'hide'}
    bError				: BOOL;
    {attribute 'hide'}
    nErrorId			: UDINT:=0;
    {attribute 'hide'}
    sMessage : T_MAXSTRING;
    {attribute 'hide'}
    bSaveEnabled	: BOOL := TRUE;
    {attribute 'hide'}
    bDone : BOOL;
    {attribute 'hide'}
    bBusy    : BOOL;
END_VAR

VAR PERSISTENT
    {attribute 'conditionalshow'}
    bSaved: BOOL;
    {attribute 'conditionalshow'}
    fSavedPosition: LREAL;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[IF NOT __ISVALIDREF(AxisRef) THEN
    RETURN;
END_IF

AxisRef.ReadStatus();

RestoreAxisParams();

// Save Encoder position
// needs some pre-conditions to use persistent storage
IF bSaveEnabled THEN
    // Check ST_MotionStage for an encoder error (range 0x44nn)
    bEncError:= AxisRef.Status.Error AND  ( AxisRef.Status.ErrorID  >= 16#4400  AND AxisRef.Status.ErrorID <= 16#44FF );
    // Do not save if we're currently loading or if there is an encoder error
    IF NOT bRestoreLoad AND NOT bEncError AND NOT bRestoreWaitRetry THEN
        fSavedPosition := AxisRef.NcToPlc.ActPos;
        // This persistent variable lets us check if anything was saved
        // It will be TRUE at startup if we have saved values
        bSaved := TRUE;
        (*
            use this with a timer of a change threshold on the fActPosition
            to trigger a save from this PLC program. otherwise this will be a spamming
            persistence, filling up the disk.
        *)
        //iPersistentDataStorage.TriggerWriteOfPersistentData := bSaveEnabled;
    END_IF
END_IF]]></ST>
    </Implementation>
    <Property Name="Busy" Id="{e5dec030-d598-46fd-afca-22fd9bde3a46}">
      <Declaration><![CDATA[PROPERTY Busy : BOOL
]]></Declaration>
      <Get Name="Get" Id="{51885e31-af8a-4a94-a215-169272d4b781}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[Busy := bBusy;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="CallAfterInit" Id="{1d48b6b6-5d18-47cf-8816-5058ff3a06eb}">
      <Declaration><![CDATA[METHOD PUBLIC CallAfterInit
VAR_INPUT
    AxisRef : REFERENCE TO AXIS_REF;
    iPersistentDataStorage : I_PersistentDataStorage;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.AxisRef REF= AxisRef;
THIS^.iPersistentDataStorage := iPersistentDataStorage;
]]></ST>
      </Implementation>
    </Method>
    <Property Name="Done" Id="{a3a56a42-9da7-40a8-925f-55df107c55cd}">
      <Declaration><![CDATA[PROPERTY Done : BOOL
]]></Declaration>
      <Get Name="Get" Id="{ec9b7af9-7f82-4fed-872e-fdb89505987f}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[Done := bRestoreDone;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="Error" Id="{239ac2b3-3c2a-4399-a932-e6c718d2e1bb}">
      <Declaration><![CDATA[PROPERTY Error : BOOL
]]></Declaration>
      <Get Name="Get" Id="{4e85ac84-f8ba-45e9-84c6-7c5588481cb5}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[Error := bError;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="ErrorID" Id="{7fc4dd77-9990-47fb-a3f3-15f5d1c36a93}">
      <Declaration><![CDATA[PROPERTY ErrorID : UDINT
]]></Declaration>
      <Get Name="Get" Id="{d39cb682-a948-4eed-b151-61c04c28fc87}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[ErrorID := nErrorID;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="FB_Init" Id="{6c116c09-ef45-432c-8e07-3fe37fad5966}">
      <Declaration><![CDATA[//FB_Init is always available implicitly and it is used primarily for initialization.
//The return value is not evaluated. For a specific influence, you can also declare the
//methods explicitly and provide additional code there with the standard initialization
//code. You can evaluate the return value.
METHOD FB_Init: BOOL
VAR_INPUT
    bInitRetains: BOOL; // TRUE: the retain variables are initialized (reset warm / reset cold)
    bInCopyCode: BOOL;  // TRUE: the instance will be copied to the copy code afterward (online change)
    AxisRef : REFERENCE TO AXIS_REF;
    iPersistentDataStorage : I_PersistentDataStorage;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.AxisRef REF= AxisRef;
THIS^.iPersistentDataStorage := iPersistentDataStorage;
]]></ST>
      </Implementation>
    </Method>
    <Property Name="Message" Id="{32b322a0-2932-4424-b95e-453b6c119c35}">
      <Declaration><![CDATA[PROPERTY Message : T_MAXSTRING
]]></Declaration>
      <Get Name="Get" Id="{e135d05b-80a3-4ab1-9f4a-39e3dfbcfa43}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[Message := sMessage;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="RestoreAxisParams" Id="{68aa741c-8d0b-49b0-a5c7-e5cb1bf9c532}">
      <Declaration><![CDATA[METHOD RestoreAxisParams
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// Trigger a load if anything was saved at all
IF NOT bRestoreInit THEN
    bRestoreInit := TRUE;
    bRestoreLoad S= bSaved;
    fbSetPos.Options.ClearPositionLag := TRUE;
END_IF

// Set our position if bRestoreLoad is true
fbSetPos( Axis:=AxisRef, Execute:=bRestoreLoad, Position:=fSavedPosition);

// Only load once, at startup
bRestoreLoad R= fbSetPos.Done OR fbSetPos.Error;

IF fbSetPos.Error THEN
    // Keep the error latched, it can disappear if Execute is set to FALSE
    nLatchError := fbSetPos.ErrorID;
    nCurrTries := nCurrTries + 1;
    IF nCurrTries >= nMaxRetries THEN
        // Alert the user that something has gone wrong
        bError := TRUE;
        nErrorId := nLatchError;
        sMessage := 'Error loading previously saved position.';
    ELSE
        // Reset the FB for the next retry
        fbSetPos( Axis:=THIS^.AxisRef, Execute:=bRestoreLoad, Position:=fSavedPosition);
        // Try again
        bRestoreWaitRetry := TRUE;
    END_IF
ELSE
    bRestoreDone := TRUE;
END_IF

tonRestoreRetry( IN := bRestoreWaitRetry, PT := tRetryDelay);
bRestoreLoad S= tonRestoreRetry.Q;
bRestoreWaitRetry R= tonRestoreRetry.Q;]]></ST>
      </Implementation>
    </Method>
    <Method Name="SaveAxisParams" Id="{29eaeb35-c218-4604-adff-fca8f06a68eb}">
      <Declaration><![CDATA[METHOD SaveAxisParams
VAR_INPUT
    Enable	: BOOL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.bSaveEnabled := Enable;
]]></ST>
      </Implementation>
    </Method>
    <Property Name="SavedPosition" Id="{4a2e7fd2-dff9-49f3-8bb0-6e6d2bbd5bd1}">
      <Declaration><![CDATA[PROPERTY SavedPosition : LREAL
]]></Declaration>
      <Get Name="Get" Id="{47036862-e42a-472d-9c02-661c5f9aa886}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[SavedPosition := fSavedPosition;]]></ST>
        </Implementation>
      </Get>
    </Property>
  </POU>
</TcPlcObject>